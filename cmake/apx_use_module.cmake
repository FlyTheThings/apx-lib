function(apx_use_module module)
    get_property(modules GLOBAL PROPERTY APX_MODULES)
    if(NOT ${module} IN_LIST modules)
        string(REPLACE "." ";" module_path_list ${module})
        string(REPLACE "." "/" module_path ${module})
        list(GET module_path_list 0 type)

        set(dest_path modules/${module_path})

        if(type STREQUAL "shared")
            list(POP_FRONT module_path_list)
            list(JOIN module_path_list "/" module_path)
            get_filename_component(module_path ${APX_SHARED_DIR}/${module_path} ABSOLUTE)
        else()
            get_filename_component(module_path ${APX_MODULES_DIR}/${module_path} ABSOLUTE)
        endif()

        if(EXISTS "${module_path}/CMakeLists.txt")
            get_property(module_paths GLOBAL PROPERTY APX_MODULES_PATHS)
            if(NOT ${module_path} IN_LIST module_paths)
                set_property(GLOBAL APPEND PROPERTY APX_MODULES_PATHS ${module_path})
                add_subdirectory(${module_path} ${dest_path})
            endif()
        else()
            message(FATAL_ERROR "Module not found: ${module} (${module_path})")
        endif()
    endif()
endfunction()
